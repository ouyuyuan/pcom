
! Description: dealing with density field
!
!      Author: OU Yuyuan <ouyuyuan@lasg.iap.ac.cn>
!     Created: 2015-10-03 07:41:56 BJT
! Last Change: 2015-10-04 07:04:51 BJT

module mod_den

  use mod_kind, only: wp

  implicit none
  private

  public &
    den_rho
    
contains !{{{1

function den_rho (t, s, p0) !{{{1
  ! Calculates the density of seawater using the
  !   standard equation of state recommended by unesco(1981).
  !	  Coefficients of rho0 is given by \citet{Millero1981}
  !	  Coefficients of k is according to \citet{Jackett1995}

  real (kind=wp), intent(in) :: &
    t  ,& ! potential temperature, degrees Celcius
    s  ,& ! salinity, practical salinity units
    p0    ! sea pressure (preclude surface atmaspheric pressure), in Pa

  real (kind=wp) :: den_rho

  real (kind=wp) :: p, p2, & 
    t2, t3, t4, t5, &
    s32, s2, &
    r0, r, a, b, k

  ! convert Pa to bar
  p = p0 * 1e-5

  p2  = p*p
  t2  = t*t
  t3  = t2*t
  t4  = t3*t
  t5  = t4*t
  s32 = s*sqrt(s)
  s2  = s*s

  r0 = 999.842594 + 6.793952e-2*t - 9.095290e-3*t2  &
       + 1.001685e-4*t3 - 1.120083e-6*t4 + 6.536336e-9*t5
  a  = 8.24493e-1 - 4.0899e-3*t + 7.6438e-5*t2 - 8.2467e-7*t3 + 5.3875e-9*t4
  b  = -5.72466e-3 + 1.0227e-4*t - 1.6546e-6*t2

  r  = r0 + a*s + b*s32 + 4.8314e-4 * s2

  k  =   1.965933e4  + 1.444304e2 *t - 1.706103   *t2 + 9.648704e-3*t3 - 4.190253e-5*t4  &
     + ( 5.284855e1  - 3.101089e-1*t + 6.283263e-3*t2 - 5.084188e-5*t3 ) * s &
     + ( 3.886640e-1 + 9.085835e-3*t - 4.619924e-4*t2 ) * s32  &
     + ( 3.186519    + 2.212276e-2*t - 2.984642e-4*t2 + 1.956415e-6*t3 ) * p &
     + ( 6.704388e-3 - 1.847318e-4*t + 2.059331e-7*t2 ) * s * p &
     +   1.480266e-4*s32*p  &
     + ( 2.102898e-4 - 1.202016e-5*t + 1.394680e-7*t2 ) * p2 &
     + (-2.040237e-6 + 6.128773e-8*t + 6.207323e-10*t2) * s * p2

  den_rho = r / ( 1.0 - p / k )
end function den_rho

end module mod_den !{{{1
!-------------------------------------------------------{{{1
! vim:fdm=marker:fdl=0:
! vim:foldtext=getline(v\:foldstart).'...'.(v\:foldend-v\:foldstart):
